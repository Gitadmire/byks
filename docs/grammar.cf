<Program>      := "int" "main" "(" ")" <CompoundStmt>
<CompoundStmt> := "{" <StmtList> "}"
<StmtList>     := <Stmt> <StmtList>
               |  "<e>"
<Stmt>         := <WhileStmt>
	       |  <IfStmt>
               |  <CompoundStmt>
               |  <Declaration>
               |  <Expr> ";"
               | ";"
<WhileStmt>    := "while" "(" <Expr> ")" <Stmt>
<IfStmt>       := "if" "(" <Expr> ")" <Stmt> <ElsePart>
<ElsePart>     := "else" <Stmt>
               |  "<e>"
<Declaration>  := <Type> <IdentList> ";"
<Type>         := "int"
               |  "float"
<IdentList>    := "Identifier" "," <IdentList>
<Expr>	       := <ExprNLR> <LogicOp> <Expr>
	       |  <ExprNLR>
<ExprNLR>      := "Identifier" ":=" <Expr>
               |  <NotOp> <Expr>
               |  <Rvalue>
<LogicOp>      := "&&"
               |  "||"
<NotOp>        := "!"
<Rvalue>       := <Mag> <Compare> <Mag>
	       |  <Mag>	
<Compare>      := "=="
               |  "<"
               |  ">"
<Mag>          := <Term> <Mag1>
<Mag1>         := "+" <Term> <Mag1>
               |  "-" <Term> <Mag1>
               |  "<e>"
<Term>         := <Factor> <Term1>
<Term1>        := "*" <Factor> <Term1>
               |  "/" <Factor> <Term1>
               |  "<e>"
<Factor>       := "(" <Expr> ")"
               |  "Identifier"
               |  <Number>
<Number>       := "LitInt"
               |  "-" "LitInt"
               |  "LitFloat"
               |  "-" "LitFloat"
